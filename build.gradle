plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.12' apply false
    id 'io.spring.dependency-management' version '1.1.3' apply false
}


java {
    sourceCompatibility = '20'
}


repositories {
    mavenCentral()
}

subprojects {
    group = 'com.project'

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.hibernate:hibernate-core:6.2.7.Final'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
        annotationProcessor "jakarta.annotation:jakarta.annotation-api"
        annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    }

    test {
        useJUnitPlatform()
    }
}

project(":customer") {
    dependencies {
        implementation(project(":common"))
    }
}